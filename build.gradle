buildscript {
    ext {
        springBootVersion = '2.1.9.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

group 'org.hyperledger'
version '0.8.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
    // for vcx java wrapper
    maven {
        url "http://13.125.219.189/repository/maven-public/" // skt-develop repo
    }
    // (temporal) indy java wrapper - for register_did
    maven {
        url "https://repo.sovrin.org/repository/maven-public" // sovrin repo
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web') {
        exclude module: "logback-classic"
    }
    testCompile('org.springframework.boot:spring-boot-starter-test') {
        exclude module: "logback-classic"
    }

    // wrapper for vcx library
    implementation 'com.evernym:vcx:0.8.2-sktston-SNAPSHOT' // use maven repository
    //implementation fileTree(dir: '../build/libs', include: ['*.jar']) // use local lib

    // (temporal) wrapper for indy library - for register_did
    implementation 'org.hyperledger:indy:1.15.0'

    // logger
    implementation 'org.slf4j:slf4j-simple:1.7.25'

    implementation 'net.java.dev.jna:jna:4.5.0'
    implementation 'com.jayway.jsonpath:json-path:2.4.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'commons-cli:commons-cli:1.4'

    // for WebClient
    compile ('org.springframework.boot:spring-boot-starter-webflux') {
        exclude module: "logback-classic"
    }
    compile 'org.projectreactor:reactor-spring:1.0.1.RELEASE'
}

task faber(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'Faber'
}
task faber_pg(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'Faber'
    args(['--postgres'])
}
task alice(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'Alice'
    standardInput = System.in
}
task alice_pg(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'Alice'
    standardInput = System.in
    args(['--postgres'])
}
task register_did(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'RegisterDid'
}
task webhook_faber(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'webhook.faber.Application'
    args(['--spring.config.location=classpath:/application-faber.properties'])
}
task webhook_alice(type:JavaExec) {
    group 'application'
    classpath sourceSets.main.runtimeClasspath
    main = 'webhook.alice.Application'
    args(['--spring.config.location=classpath:/application-alice.properties'])
}